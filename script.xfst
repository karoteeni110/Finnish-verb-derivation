clear stack

! Vokaalit ja konsonantit Suomen kielessä
define Vowel    a | o | u | ä | ö | y | e | i ;
define Con      b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | z ;
define AA       ä | a ;
define UU       u | y ;

! Umpitavut; lähteet: http://scripta.kotus.fi/visk/sisallys.php?p=11 
define Umpitavu [ Con Vowel Con ] | [ Con Vowel Con Con ] | [ Con Vowel Vowel Con ] |
                 [ Vowel Con ] | [ Vowel Con Con ] | [ Vowel Vowel Con ] ;

! Avotavut; 
define Avotavu     [ Con Vowel ] | [ Vowel ] | [ Vowel Vowel ] | [ Con Vowel Vowel ] ;

! Viimeinen avotavu
define LoppuAvotavu     [ Con AA ] | [ AA ] | [ AA AA ] | [ Con AA AA ] ;

! Loosely, we define verbs are words consisting of less than 6 syllables, 
! with the last one being dA/ A/ tA/ lA
define FinVerb      [ [Avotavu] | [Umpitavu] ]^<5 [ [ d AA ] | [ AA ] | [ t AA ] | [ l A ] ] ; 

read lexc Derivation.lexc ;
substitute defined FinVerb for ^GUESSVERB 
define AllFinVerbs ;

!!!!!
!!!!! RULES
!!!!!

! Mark the weak grade for type 1 verbs with -Ahta, -skele, and -ttA verbs
! Partly adapted from Gradation.script (Karttunen, 2004) and ASTEVAIHTELUTYYPIT (KOTUS 2018)
define weakMark    k -> "^WK^", p -> "^WP^", t -> "^WT^" || \s _ Vowel AA %- [ AA h t AA AA | s k e l l AA | t t AA AA] ; 
                        ! Weak mark for Type 1 verbs; "sk" "sp" and "st" remains unchanged
                        ! antaa > ant^Weak^aa-AhtA
define weakK        "^WK^" -> j || r _ e          ! kulje-AhtA
                        .o.
                    "^WK^" -> g || n _            ! tunge-AhtA
                        .o.
                    "^WK^" -> k || [ h | t ] _           ! k stays when proceeded by h or t
                        .o.
                    "^WK" -> 0 ;                    ! hae-AhtA
define weakP        "^WP^" -> m || m _              ! emmi-AhtA
                        .o.
                    "^WP^" -> 0 || p _              ! hypi-AhtA
                        .o.
                    "^WP^" -> v ;                  ! viivy-AhtA

define weakT        [ "^WT^" -> n || n _ ]           ! myönnä-AhtA
                        .o.
                    [ "^WT^" -> l || l _ ]         ! yllä-AhtA
                        .o.
                    [ "^WT" -> r || r _ ]              ! kerro-AhtA
                        .o.
                    [ "^WT^" -> 0 || t _ ]             ! saata-AhtA
                        .o.
                     [ "^WT^" -> d ];              ! pidä-AhtA
define weakGrade    weakMark .o. weakK .o. weakP .o. weakT ;

! Strong gradation for type 4 and 6
! Applicable for -ttA, -Aise, and -ile suffixes
define strongGrade   k -> k k , p -> p p, t -> t t, n g -> n k, 
                    v -> p, d -> t , 0 -> ( k ) , m m -> m p,
                    h j -> h k || Vowel _ Vowel t AA %- [ t t AA AA | AA i s t AA | i l l AA | UU t UU AA ] ;

define Gradation weakGrade .o. strongGrade ;

! Stemming without gradation;
! This is a set of public rules and is applied to all of the verbs;
! Hat mark (^) to avoid syllable clashes
define stemI       AA -> %^ || Vowel _ %- ;    ! "antaa > anta-"; 
define stemII      AA -> %^ || d _ %- ;          ! "luoda > luo-"; 
define stemIII     l l AA -> l %^ , n n AA -> n %^, r r AA -> r %^, s t AA -> s %^ || _ %- ;         ! "tulla > tul-";
define stemIV      t AA -> %^ || Vowel _ %-  ;    ! "ruveta > ruve"; 
define stemV      i t AA -> %^ || _ %-  ;      ! "merkitä > merki-"; 
define CleanOffHat      %^ -> 0 ;
define Stemming    stemI .o. stemII .o. stemIII .o. stemIV .o. stemV .o. CleanOffHat ; 

! -UtU-; kääntä-ytyä > kääntä-ytyä; sopia-utua > sope-utu-a
! A and e stay in the end of the stem, i turns into e 
define UTU     [ a a ] -> a  , [ e e ] -> e , [ ä ä ] -> ä , i -> e || _  %- UU t UU AA ;

! -AhtA- 
! Drop the last vowel
define AHTA       Vowel -> 0 ||  _  %- AA h t AA AA ; 

! -ile-; Vartalonloppuiset A ja e säilyvät ile-verbinjohdinten edellä
!
define ILE        AA -> 0 , i -> e, e e -> e , o o -> o, 
                    i i -> i, u u -> u, y y -> y ||  _  %- i l l AA ;

! -skele-
define SKELE        a -> [ e | i ] || _  %- s k e l l AA ; ! Not predictable              

! -tta-
define TTA          a -> o, ä -> [e | ä] || _  %- t t AA AA ; ! Not predictable

! -aise-
! Drop all of the short vowels, and long vowels at the end of the type 4 and 6 verb roots 
define AISE         Vowel -> 0 || Con _  %- AA i s t AA ; 
       

define VokaalisoituOne   A -> a, U -> u || $[ a | o | u ] _ ; ! No need to consider compounds
define VokaalisoituTwo   A -> ä, U -> y ;
define CleanOffdash      %- -> 0 ;

regex AllFinVerbs .o. VokaalisoituOne .o. VokaalisoituTwo .o. Gradation .o. Stemming
                  .o. UTU .o. AHTA .o. ILE .o. SKELE .o. TTA .o. AISE
                  .o. CleanOffdash ;